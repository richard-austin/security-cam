plugins {
    id 'groovy'
    id 'war'
    id "org.springframework.boot" version "3.3.4"
    id 'io.spring.dependency-management' version '1.1.6'
    id("com.github.erdi.webdriver-binaries") version "3.2"
}

group = "server"
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://build.shibboleth.net/maven/releases/")  // For implementation group: 'org.opensaml', name: 'opensaml-xacml-impl', version: '4.3.0'
        //  url = uri("https://company/com/maven2")
    }
}

springBoot {
    mainClass.set("com.securitycam.ServerApplication")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-groovy-templates'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.groovy:groovy'
    // https://mvnrepository.com/artifact/org.apache.groovy/groovy-json
    implementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.23'
    implementation 'org.springframework.boot:spring-boot-starter-websocket:3.3.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.2'
    implementation 'org.projectlombok:lombok:1.18.34'


    implementation 'com.h2database:h2:2.2.224'
//    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'org.springframework.security:spring-security-core:6.3.3'
    implementation 'org.springframework.security:spring-security-web:6.3.3'
    implementation 'org.springframework.security:spring-security-config:6.3.3'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'org.springframework.security:spring-security-crypto:6.3.3'
    implementation group: 'org.jboss.aerogear', name: 'aerogear-otp-java', version: '1.0.0'
    // https://mvnrepository.com/artifact/jakarta.annotation/jakarta.annotation-api

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-configuration-processor:3.3.2'
    implementation 'io.netty:netty-all:4.1.111.Final'
    implementation 'javax.sdp:nist-sdp:1.0'
    implementation 'com.google.code.gson:gson:2.10'

    implementation (group: 'org.eclipse.angus', name: 'angus-mail', version: '2.0.3')
    implementation group: 'jakarta.mail', name: 'jakarta.mail-api', version: '2.1.3'

    // Onvif dependencies
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxws', version: '3.3.2'
    implementation group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'
    implementation group: 'javax.jws', name: 'javax.jws-api', version: '1.1'
    implementation group: 'org.apache.cxf.services.wsn', name: 'cxf-services-wsn-core', version: '3.3.2'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.10'
    implementation 'commons-io:commons-io:2.7'

    // Updated to fix the older version (2.2.3) breaking email
    implementation group: 'org.apache.wss4j', name: 'wss4j-ws-security-common', version: '3.0.3'
    implementation group: 'org.opensaml', name: 'opensaml-xacml-impl', version: '4.3.0'

    implementation group: 'org.apache.maven', name: 'maven-model-builder', version: '3.6.1'
    implementation group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.1'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.1'
    implementation group: 'javax.activation', name: 'activation', version: '1.1.1'
    implementation group: 'com.sun.xml.messaging.saaj', name: 'saaj-impl', version: '1.5.1'


    // TODO: Upgrading these causes java.lang.IncompatibleClassChangeError: class org.apache.cxf.jaxws.WrapperClassGenerator has interface org.apache.cxf.common.util.ASMHelper as super class
    implementation 'org.apache.cxf.xjc-utils:cxf-xjc-runtime:3.3.1'
//    implementation 'org.apache.cxf:cxf-rt-ws-security:3.3.2'
    implementation 'org.apache.cxf:cxf-rt-transports-http:3.3.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

}

bootRun {
    jvmArgs('-Dspring.profiles.active=dev')
}

tasks.named('test') {
    useJUnitPlatform()
}

//assetCompile.dependsOn(':server:setVersion')
bootWar.dependsOn(':client:buildClient',':server:setVersion')

bootWar {
    webInf {
        from('../client/dist/client/browser')
        include '**/*'
        into('classes/static')
    }
}

tasks.register('setVersion', Exec) {
    group = 'build'
    description = 'Sets the version.txt file with the current version from git describe'
    workingDir "$projectDir"
    executable "sh"
    args = ['-c', 'mkdir -p version && git describe --tags --dirty | tr -d \'\\n\' > src/main/resources/version.txt']
}

tasks.register('buildDebFile', Exec) {
    group='build'
    dependsOn([':server:assemble', ':generateProductKey:uberJar', ':fmp4-ws-media-server:deploy', ':initialAdmin:buildWar'])
    group = 'build'
    workingDir "$projectDir/../xtrn-scripts-and-config/deb-file-creation"
    commandLine './create-deb.sh'
}

//tasks.withType(Copy) {
//    duplicatesStrategy = DuplicatesStrategy.INHERIT
//}

tasks.withType(GroovyCompile).configureEach {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

webdriverBinaries {
    chromedriver {
        versionRegexp = '.*'  // Use latest version
    }

    geckodriver '0.24.0'
    edgedriver '86.0.601.0'
}

tasks.withType(Test).configureEach {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

//assets {
//    minifyJs = true
//    minifyCss = true
//}
