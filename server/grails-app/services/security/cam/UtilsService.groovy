package security.cam

import asset.pipeline.grails.AssetResourceLocator
import com.google.gson.GsonBuilder
import grails.config.Config
import grails.core.GrailsApplication
import grails.gorm.transactions.Transactional
import grails.util.Environment
import org.grails.web.json.JSONObject
import org.intellij.lang.annotations.Language
import org.springframework.core.io.Resource
import org.springframework.messaging.simp.SimpMessagingTemplate
import org.springframework.scheduling.annotation.EnableScheduling
import org.springframework.scheduling.annotation.Scheduled
import security.cam.audiobackchannel.RtspClient
import security.cam.commands.SMTPData
import security.cam.commands.SetupSMTPAccountCommand
import security.cam.commands.StartAudioOutCommand
import security.cam.enums.PassFail
import security.cam.interfaceobjects.ObjectCommandResponse
import server.CameraAdminCredentials

import java.nio.charset.StandardCharsets
import java.nio.file.FileSystems
import java.nio.file.Files
import java.nio.file.LinkOption
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.attribute.GroupPrincipal
import java.nio.file.attribute.PosixFileAttributeView
import java.nio.file.attribute.UserPrincipalLookupService
import java.util.concurrent.ConcurrentLinkedQueue

class Temperature {
    Temperature(String temp) {
        this.temp = temp
    }

    String temp
}

class Version {
    Version(String version) {
        this.version = version
    }

    String version
}

class MyIP {
    MyIP(String myIp) {
        this.myIp = myIp
    }

    String myIp
}

@Transactional
@EnableScheduling
class UtilsService {
    LogService logService
    AssetResourceLocator assetResourceLocator
    GrailsApplication grailsApplication
    SimpMessagingTemplate brokerMessagingTemplate
    CamService camService
    Queue<byte[]> audioQueue = new ConcurrentLinkedQueue<>()

    public final passwordRegex = /^[A-Za-z0-9][A-Za-z0-9(){\[1*Â£$\\\]}=@~?^]{7,31}$/
    public final usernameRegex = /^[a-zA-Z0-9](_(?!(.|_))|.(?!(_|.))|[a-zA-Z0-9]){3,18}[a-zA-Z0-9]$/
    public final emailRegex = /^([a-zA-Z0-9_\-\.]+)@((\[[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}\.)|(([a-zA-Z0-9\-]+\.)+))([a-zA-Z]{2,4}|[0-9]{1,3})(\]?)$/
    public static final onvifBaseAddressRegex = /^((([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])\.){3}([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5]))($|:([0-9]{1,4}|6[0-5][0-5][0-3][0-5])$)/

    /**
     * executeLinuxCommand: Execute a linux command
     * @param command the command and its parameters as a string
     * @return: The returned value
     */
    String executeLinuxCommand(String command) {
        Process p = new ProcessBuilder(command).start() // Process p = Runtime.getRuntime().exec(command)
        String retVal = processCommandOutput(p)
        p.waitFor()
        retVal
    }

    /**
     * executeLinuxCommand: Execute a linux command
     * @param command the command and its parameters as a string array (used for specifying a shell to execute the command)
     * @return: The returned value
     */
    String executeLinuxCommand(String[] command) {
        Process p = new ProcessBuilder(command).start() // Runtime.getRuntime().exec(command)
        String retVal = processCommandOutput(p)
        p.waitFor()
        retVal
    }

    private static String processCommandOutput(Process p) {
        BufferedReader reader =
                new BufferedReader(new InputStreamReader(p.getInputStream()))
        StringBuffer sb = new StringBuffer()
        String line
        while ((line = reader.readLine()) != null) {
            sb.append(line + "\n")
        }

        return sb.toString()
    }

    /**
     * getTemperature: Get the core temperature (Raspberry pi only). This is called at intervals to keep the session alive
     * @return: The temperature as a string. On non Raspberry pi systems an error is returned.
     */
    def getTemperature() {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            if (Environment.isDevelopmentMode()) {
                // There is no vcgencmd on a PC, only raspberry pi
                Temperature temp = new Temperature("temp=45.2'C")
                result.responseObject = temp
            } else {
                Temperature temp = new Temperature(executeLinuxCommand("vcgencmd", "measure_temp"))
                result.responseObject = temp
            }
        }
        catch (Exception ex) {
            logService.cam.error("Exception in getTemperature: " + ex.getCause() + ' ' + ex.getMessage())
            result.status = PassFail.FAIL
            result.error = ex.getMessage()
        }
        return result
    }

    /**
     * getVersion: Get the version from the config file application.yml. This version is generated by
     *             by git describe --tags
     * @return: The version string
     */
    def getVersion() {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            Resource verRes = assetResourceLocator.findResourceForURI('./version.txt')
            String verStr = new String(verRes?.getInputStream()?.bytes, StandardCharsets.UTF_8)
            Version ver = new Version(verStr)
            result.responseObject = ver
        }
        catch (Exception ex) {
            logService.cam.error("Exception in getVersion: " + ex.getCause() + ' ' + ex.getMessage())
            result.status = PassFail.FAIL
            result.error = ex.getMessage()
        }
        return result
    }

    def getOpenSourceInfo() {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            @Language("HTML")
            String response = '''
        <h2>Angular web framework v12.0.5</h2>
        <a href="https://angular.io" target="_blank">Angular</a>
        <hr>
        <h2>Angular Material v12.0.5</h2>
        <a href="https://material.angular.io/" target="_blank">Angular Material</a>
        <hr>
        <h2>Angular Forms v12.0.5</h2>
        <a href="https://angular.io/guide/forms-overview/" target="_blank">Angular Forms</a>
        <hr>
        <h2>Stomp.js v7.0.0</h2>
        <a href="https://www.npmjs.com/package/@stomp/stompjs" target="_blank">Stomp.js</a>
        <hr>
        <h2>File Saver v2.0.5</h2>
        <a href="https://www.npmjs.com/package/@types/file-saver" target="_blank">File Saver</a>
        <hr>
        <h2>hls.js v1.0.7</h2>
        <a href="https://www.npmjs.com/package/hls.js?utm_source=cdnjs&utm_medium=cdnjs_link&utm_campaign=cdnjs_library" target="_blank">Hls.js</a>
        <hr>
        <h2>moment v2.29.1</h2>
        <a href="https://www.npmjs.com/package/moment" target="_blank">moment</a>
        <hr>
        <h2>Object Hash v3.0.0</h2>
        <a href="https://www.npmjs.com/package/object-hash" target="_blank">Object Hash</a>
        <hr>
        <h2>rx v4.1.0</h2>
        <a href="https://www.npmjs.com/package/rx" target="_blank">rx</a>
        <hr>
        <h2>rxjs v6.6.0</h2>
        <a href="https://www.npmjs.com/package/rxjs" target="_blank">rxjs</a>
        <hr>
        <h2>rxjs-observe v2.1.5</h2>
        <a href="https://www.npmjs.com/package/rxjs-observe" target="_blank">rxjs-observ</a>
        <hr>
        <h2>tslib v2.0.0</h2>
        <a href="https://www.npmjs.com/package/tslib" target="_blank">tslib</a>
        <hr>
        <h2>zone.js v0.11.4</h2>
        <a href="https://www.npmjs.com/package/zone.js?activeTab=readme" target="_blank">zone.js</a>
        <hr>
        <h2>Grails v5.3.2</h2>
        <a href="https://grails.org/" target="_blank">Grails</a>
        <hr>
        <h2>Java OpenJDK v19.0.2</h2>
        <a href="https://openjdk.org/" target="_blank">Java OpenJDK</a>
        <hr>
        <h2>ffmpeg v4.4.4</h2>
        <a href="https://www.ffmpeg.org/" target="_blank">ffmpeg</a>
        <hr>
        <h2>Onvif for Java</h2>
        <a href="https://github.com/fpompermaier/onvif" target="_blank">Onvif for Java</a>
        <hr>
        <h2>Motion Service v4.5.1 (for motion detection)</h2>
        <a href="https://motion-project.github.io/" target="_blank">Motion Service</a>
        <hr>
        <h2>nginx v1.22.0 (Reverse proxy)</h2>
        <a href="https://nginx.org/en/" target="_blank">nginx</a>
        <hr>
        <h2>Apache Tomcat vTomcat/9.0.70 (Web Server)</h2>
        <a href="https://tomcat.apache.org/" target="_blank">Apache Tomcat</a>
        <hr>
        <h2>libraspberrypi-bin</h2>
        <a href="https://packages.ubuntu.com/focal-updates/misc/libraspberrypi-bin" target="_blank">libraspberrypi-bin</a>
        <hr>
        <h2>Chrony v4.3 (NTP Server)</h2>
        <a href="https://chrony-project.org/news.html" target="_blank">Chrony</a>
        <hr>
        <h2>Ubuntu network-manager v1.42.4</h2>
        <a href="https://ubuntu.com/core/docs/networkmanager" target="_blank">Network Manager</a>
        <hr>
        <h2>wireless-tools v30 (Wireless Tools for Linux)</h2>
        <a href="https://hewlettpackard.github.io/wireless-tools/Tools.html" target="_blank">Wireless Tools</a>
        <hr>
        <h2>moreutils</h2>
        <a href="https://ostechnix.com/moreutils-collection-useful-unix-utilities/" target="_blank">Moreutils</a>
        <hr>
        <h2>Python3 v 3.11.4</h2>
        <a href="https://www.python.org/" target="_blank">Python3</a>
        <hr>
    '''
            result.response = response

        }
        catch (Exception ex) {
            logService.cam.error("Exception in getOpenSourceInfo: " + ex.getCause() + ' ' + ex.getMessage())
            result.status = PassFail.FAIL
            result.error = ex.getMessage()
        }
        return result
    }

    /**
     * setIP: Set the file myip to contain our current public ip address.
     * @return: Our public ip address
     */
    ObjectCommandResponse setIP() {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            InputStream is = new URL("https://api.ipify.org").openStream()
            Scanner s = new Scanner(is, "UTF-8").useDelimiter("\\A")

            String baseDir = grailsApplication.config.camerasHomeDirectory
            Path myipFile = Paths.get(baseDir as String, 'myip')

            String myIp = s.next()
            //Write the ip address to the file
            BufferedWriter writer = new BufferedWriter(new FileWriter(myipFile.toString()))
            writer.write(myIp)
            writer.close()
            s.close()
            is.close()
            result.responseObject = new MyIP(myIp)

            // Make the myip file a member of the security-cam group
            String secCam = "security-cam"
            UserPrincipalLookupService lookupService = FileSystems.getDefault()
                    .getUserPrincipalLookupService()
            GroupPrincipal group = lookupService.lookupPrincipalByGroupName(secCam)
            Files.getFileAttributeView(myipFile, PosixFileAttributeView.class,
                    LinkOption.NOFOLLOW_LINKS).setGroup(group)
        }
        catch (IOException e) {
            logService.cam.error "${e.getClass().getName()} in setIP: ${e.getMessage()}"
            result.status = PassFail.FAIL
            result.error = "${e.getClass().getName()} -- ${e.getMessage()}"
        }
        return result
    }

    ObjectCommandResponse setupSMTPClient(SetupSMTPAccountCommand cmd) {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            def gson = new GsonBuilder()
                    .setPrettyPrinting()
                    .create()

            String res = gson.toJson(cmd.getData())
            def writer = new BufferedWriter(new FileWriter("/var/security-cam/smtp.json"))
            writer.write(res)
            writer.close()
        }
        catch (Exception e) {
            logService.cam.error "${e.getClass().getName()} in setupSMTPClient: ${e.getMessage()}"
            result.status = PassFail.FAIL
            result.error = "${e.getClass().getName()} -- ${e.getMessage()}"
        }
        return result
    }

    ObjectCommandResponse getSMTPClientParams() {
        ObjectCommandResponse result = new ObjectCommandResponse()
        try {
            result.responseObject = getSMTPConfigData()
        }
        catch (FileNotFoundException fnf) {
            final String noConfigFile = "No existing config file for SMTP client. It will be created when the SMTP parameters are entered and saved."
            logService.cam.warn(noConfigFile)
            result.status = PassFail.PASS
            result.response = noConfigFile
            result.error = "${fnf.getClass().getName()} -- ${fnf.getMessage()}"
        }
        catch (Exception e) {
            logService.cam.error "${e.getClass().getName()} in getSMTPClientParams: ${e.getMessage()}"
            result.status = PassFail.FAIL
            result.response = null
            result.error = "${e.getClass().getName()} -- ${e.getMessage()}"
        }
        return result
    }

    def getSMTPConfigData() {
        Config config = grailsApplication.getConfig()
        def configFileName = config.getProperty("mail.smtp.configFile")
        File file = new File(configFileName)
        byte[] bytes = file.readBytes()
        String json = new String(bytes, StandardCharsets.UTF_8)
        def gson = new GsonBuilder().create()
        def smtpData = gson.fromJson(json, SMTPData)
        return smtpData
    }


    // Execute every second. If the audio input feed is running it increments the audioInputStreamCheckCount which is
    //  zeroed by each time an audio packet is received. If the count gets past 3, the audio feed is assumed to have
    //  stopped (maybe by the audio session not being closed cleanly), so it stops the feed properly.
    @Scheduled(fixedRate = 1000L)
    def audioFeedCheck() {
        if (audioInUse) {
            // Shut down the sudio receiver if the websocket data stream has stopped
            if (++audioInputStreamCheckCount > audioInSessionTimeout) {
                stopAudioOut()
            }
        }
    }

    boolean audioInUse = false
    RtspClient client

    private ServerSocket serverSocket
    private Socket clientSocket
    private OutputStream out
    Thread serverThread

    def startAudioOut(StartAudioOutCommand cmd) {
        ObjectCommandResponse result = new ObjectCommandResponse()
        if (!audioInUse) {
            try {
                // Set up the socket for ffmpeg backchannel feed to source from
                serverSocket = new ServerSocket(8881)
                serverThread = new Thread(() -> {
                    try {
                        clientSocket = serverSocket.accept()
                        out = clientSocket.getOutputStream()
                    }
                    catch(Exception ex) {
                        stopAudioOut()
                        logService.cam.error "${ex.getClass().getName()} in startAudioOut accept thread: ${ex.getMessage()}"
                    }
                })
                serverThread.start()

                final String talkOff = new JSONObject()
                        .put("message", "talkOff")
                        .put("instruction", "on")
                        .toString()
                // Disable audio out on clients except the initiator
                brokerMessagingTemplate.convertAndSend("/topic/talkoff", talkOff)

                final URI netcam_uri = new URI(cmd.netcam_uri)
                CameraAdminCredentials creds  = cmd.cam.credentials()
                client = new RtspClient(netcam_uri.getHost(), netcam_uri.getPort(), creds.userName, creds.password, logService)
                client.start()
                result = client.await()
            }
            catch (Exception ex) {
                stopAudioOut()
                logService.cam.error "${ex.getClass().getName()} in startAudioOut: ${ex.getMessage()}"
                result.status = PassFail.FAIL
                result.error = "${ex.getClass().getName()} -- ${ex.getMessage()}"
            }
            audioInUse = true
        }
        return result
    }

    def stopAudioOut() {
        ObjectCommandResponse result = new ObjectCommandResponse()

        try {
            final String talkOff = new JSONObject()
                    .put("message", "talkOff")
                    .put("instruction", "off")
                    .toString()
            // Re-enable audio out on clients
            brokerMessagingTemplate.convertAndSend("/topic/talkoff", talkOff)
            audioInUse = false
            audioQueue.clear()

            serverSocket?.close()
            clientSocket?.close()
            serverThread?.interrupt()
            out?.close()
            out = null
            client?.stop()
            client = null
        }
        catch (Exception ex) {
            logService.cam.error "${ex.getClass().getName()} in stopAudioOut: ${ex.getMessage()}"
            result.status = PassFail.FAIL
            result.error = "${ex.getClass().getName()} -- ${ex.getMessage()}"
        }
        return result
    }

    private final long audioInSessionTimeout = 3
    // Stop audio input session after 3 seconds without a websocket message
    private long audioInputStreamCheckCount = 0

    def audio(byte[] bytes) {
        audioQueue.add(bytes)
        // Reset the stream check count to indicate the stream is still active
        audioInputStreamCheckCount = 0
        if (out) {
            try {
                while (!audioQueue.empty) {
                    out.write(audioQueue.poll())
                    Thread.sleep(3)
                    //   System.out.println("Writing ${audioQueue.poll().length} bytes")
                }
            }
            catch (Exception ex) {
                System.out.println("${ex.getClass().getName()} in audio handler: ${ex.getMessage()}")
            }
        }
    }
}
